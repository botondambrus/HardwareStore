plugins {
    id 'war'
    id 'maven-publish'
}


dependencies {
    implementation project(':backend')
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '5.0.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.0'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'edu.bbte.idde.abim2109'
            artifactId = 'abim2109-web'
            version = '1.0-SNAPSHOT'
            from components.java
        }
    }
}

def deploymentName = 'ROOT'

def tomcatHome = System.getenv('CATALINA_HOME')
logger.quiet("Tomcat home detected as '${tomcatHome}'")


task deploy(type: Copy, dependsOn: 'war') {

    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    from war.archiveFile

    rename war.archiveFileName.get(), "${deploymentName}.war"

    into "${tomcatHome}/webapps"
}

task undeploy(type: Delete) {

    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    delete "${tomcatHome}/webapps/${deploymentName}.war"
}


